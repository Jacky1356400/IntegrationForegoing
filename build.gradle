buildscript {
  repositories {
    jcenter()
    maven { url = "http://files.minecraftforge.net/maven" }
  }
  dependencies {
    classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
  }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mc_version}-${mod_version}"
group = "jackyy.integrationforegoing"
archivesBaseName = "IntegrationForegoing"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
  sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
  version = "${forge_version}"
  runDir = "run"
  mappings = "${mappings_version}"

  replace '@FINGERPRINT@', project.findProperty('signSHA1')
  replaceIn "IntegrationForegoing.java"
}

repositories {
  maven {
    name = "JEI, TCon and Mantle Maven"
    url = "https://dvs1.progwml6.com/files/maven"
  }
  maven {
    name = "HWYLA Maven"
    url = "https://maven.tehnut.info"
  }
  maven {
    name = "TOP Maven"
    url = "https://maven.tterrag.com"
  }
  maven {
    name = "CraftTweaker Maven"
    url = "https://maven.blamejared.com"
  }
  maven {
    name = "Curse Maven"
    url = "https://www.cursemaven.com"
  }
}

dependencies {
  deobfCompile "mezz.jei:jei_1.12.2:+"
  deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
  deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:+") {
      transitive = false
  }
  deobfCompile "curse.maven:gunpowderlib-356646:3168863" //1.12.2-1.1
  deobfCompile "curse.maven:industrialforegoing-266515:2745321" //1.12.2-1.12.13-237
  deobfCompile("slimeknights:TConstruct:1.12.2-2.+") {
      transitive = false
  }
  deobfCompile "slimeknights.mantle:Mantle:1.12-1.+"
  deobfCompile "curse.maven:immersiveengineering-231951:2974106" //0.12-98
  deobfCompile "curse.maven:infinitylib-251396:2537265" //1.12.0
  deobfCompile "curse.maven:agricraft-225635:2609648" //2.12.0-1.12.0-a6
  deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.+"
  
  //Remove ID limits when working with all of the supported mods installed
  deobfCompile "curse.maven:justenoughids-296289:2919737" //1.0.3-55
}

processResources {
  inputs.property "version", project.version
  inputs.property "mcversion", project.minecraft.version
  from(sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version':project.version, 'mcversion':project.minecraft.version
  }
  from(sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
  onlyIf {
    project.hasProperty('keyStore')
  }
  keyStore = project.findProperty('keyStore')
  alias = project.findProperty('keyStoreAlias')
  storePass = project.findProperty('keyStorePass')
  keyPass = project.findProperty('keyStoreKeyPass')
  inputFile = jar.archivePath
  outputFile = jar.archivePath
}

build.dependsOn signJar

task deobfJar(type: Jar) {
  from sourceSets.main.output
  classifier = 'deobf'
}

artifacts {
  archives deobfJar
}
